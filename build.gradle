import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'eclipse'
    id 'java'
    id 'xyz.jpenilla.run-paper' version '2.1.0'
}

tasks {
    runServer {
        minecraftVersion("1.20.1")
    }
}

compileJava.options.encoding = 'Cp1252'
compileTestJava.options.encoding = 'Cp1252'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenCentral()

    //Spigot
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    // Bungeecord
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    // Skript
    maven {
        url 'https://repo.skriptlang.org/releases'
    }

    //Citizens
    maven {
        name = 'citizens-repo'
        url = 'https://maven.citizensnpcs.co/repo'
    }

}

dependencies {

    //Nullable annotation
    implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.700')

    //Spigot/Bukkit
    implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.19.4-R0.1-SNAPSHOT')

    //Skript
    implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.0-beta3') {
        transitive = false
    }

    // bStats
    shadow (group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.2')

    //Citizens
    compileOnly('net.citizensnpcs:citizens-main:2.0.30-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

}

processResources {
    filter ReplaceTokens, tokens: ["version": project.property("version")]
    from ("lang/") {
        include '*.lang'
        into 'lang/'
    }
}

shadowJar {
    dependencies {
        include(dependency('org.bstats:bstats-bukkit'))
        include(dependency('org.bstats:bstats-base'))
    }
    relocate 'org.bstats', 'me.example.addontutorial.bstats'
    configurations = [project.configurations.shadow]
    archiveVersion = project.property("version")
    minimize()
}